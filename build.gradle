plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
    id "com.moowork.node" version "1.3.1"
}

group = 'com.markekraus'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

apply plugin: "com.moowork.node"

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

node {
    version = "14.3.1"
    download = false
    nodeModulesDir = file("${project.projectDir}/fe")
}

test {
	useJUnitPlatform()
}

task reactBuild(type: NpmTask, dependsOn: npmInstall) {
    description = 'Builds the react app'
    args = ['run','build']
}

task reactPublish(type: Copy) {
    dependsOn = ['reactBuild']
    description = 'Stages react build artifacts into java static resources'
    from 'fe/build'
    into 'src/main/resources/static'
}

task cibuild(type: GradleBuild) {
    tasks = ['reactBuild', 'reactPublish', 'build']
    description = 'Runs builds in CI'
}

clean.doFirst {
    delete "src/main/resources/static/"
    println "src/main/resources/static/"
    delete "fe/build/"
    println "fe/build/"
}


bootRun.dependsOn('reactPublish')